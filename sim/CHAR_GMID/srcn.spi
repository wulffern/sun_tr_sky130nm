*idgm_lvt

*----------------------------------------------------------------
* Include
*----------------------------------------------------------------
.include ../../../design/SUN_TR_SKY130NM.spice
*----------------------------------------------------------------
* Options
*----------------------------------------------------------------
.option TNOM=27 GMIN=1e-15
*----------------------------------------------------------------
* Sources
*----------------------------------------------------------------
VSS VSS 0 dc 0
VGS VG 0 dc 0
VAC VGAC VG dc 0 ac 1
VO VO 0 dc 1
VOR VOR 0 dc 1 ac 1
*----------------------------------------------------------------
* DUT
*----------------------------------------------------------------
XM2 VO VGAC VSS VSS SUNTR_NCHDL
XM3 VOR VG VSS VSS SUNTR_NCHDL
.save i(VO) v(VG) i(VOR) i(VAC)
*----------------------------------------------------------------
* Control
*----------------------------------------------------------------
.control
unset askquit
let N = 30

let sgm = unitvec(N)
let sgds = unitvec(N)
let svgs = unitvec(N)
let sid = unitvec(N)
let sig = unitvec(N)
let index = vector(N)

foreach i $&index
 let ind = $i

 alter vgs dc = 1.8/N*ind + 26e-3*3

 dc VOR 0.5 1 0.5
 let sid[ind] = -i(vo)[1]
 let svgs[ind]= v(vg)[1]

 ac dec 50 1k 100k

 let sgm[ind] = -real(i(VO)[0])
 let sgds[ind] = -real(i(VOR)[0])

end

setplot new
let gm = sgm
settype admittance gm

let gds = sgds
settype admittance gds

let vgs = svgs
settype voltage vgs

let id = sid
settype current id
setscale id

let rds = 1/gds
let gmid = gm/id
let gmgds = gm/gds

write {cicname}.raw

quit
.endc
.end
